import org.gradle.internal.os.OperatingSystem
import org.jreleaser.model.Active

plugins {
    id 'java'
    id 'maven-publish'
}

project.ext.lwjglVersion = "3.3.6"

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        def osArch = System.getProperty("os.arch")
        if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
            project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
        } else if  (osArch.startsWith("ppc")) {
            project.ext.lwjglNatives += "-ppc64le"
        } else if  (osArch.startsWith("riscv")) {
            project.ext.lwjglNatives += "-riscv64"
        }
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {

    implementation project(":core")

    implementation project("libfrontend:libfrontend-glfw")

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-egl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-opengles"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.lwjgl:lwjgl::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-openal::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-opengles::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-stb::$lwjglNatives"

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

tasks.register('copyJar') {
    doLast {
        copy {
            from 'build/libs'
            into "../build/libs"
            include '**/*.jar'
        }
    }
}

tasks.named("build") {
    finalizedBy('copyJar')
}

publishing {
    repositories {
        maven {
            setUrl(layout.buildDirectory.dir("staging-deploy"))
        }
    }
    publications {
        create("release", MavenPublication) {
            from(components.java)
            groupId = component_group_id
            artifactId = component_artifact_id
            version = component_version

            pom {

                name.set(component_name.toString())
                description.set("Core library of Konna engine")

                url.set("https://github.com/DarthAkiraNihil/Konna")
                issueManagement {
                    url.set("https://github.com/DarthAkiraNihil/Konna/issues")
                }

                scm {
                    url.set("https://github.com/DarthAkiraNihil/Konna")
                    connection.set("scm:git://github.com/DarthAkiraNihil/Konna.git")
                    developerConnection.set("scm:git://github.com/DarthAkiraNihil/Konna.git")
                }

                licenses {
                    license {
                        name.set("The Apache Software License, Version 2.0")
                        url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                        distribution.set("repo")
                    }
                }

                developers {
                    developer {
                        id.set("AkiraNihil")
                        name.set("Egor Zverev")
                        email.set("egorzverev666@gmail.com")
                    }
                }
            }
        }
    }
}

jreleaser {
    gitRootSearch = true
    project {
        inceptionYear = "2025"
        author("@AkiraNihil")

        version=component_version
        languages {
            java {
                groupId=component_group_id.toString()
                artifactId=component_artifact_id.toString()
            }
        }
    }
    release {
        github {
            skipRelease = true
            skipTag = true
            sign = true
            branch = "master"
            branchPush = "master"
            overwrite = true
        }
    }
    signing {
        active = Active.ALWAYS
        armored = true
        verify = true
    }
    deploy {
        maven {
            mavenCentral.create("sonatype") {
                active = Active.ALWAYS
                url = "https://central.sonatype.com/api/v1/publisher"
                stagingRepository(layout.buildDirectory.dir("staging-deploy").get().toString())
                setAuthorization("Basic")
                retryDelay = 60
            }
        }
    }
}
