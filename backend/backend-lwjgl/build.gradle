import org.gradle.internal.os.OperatingSystem
import org.jreleaser.model.Active

plugins {
    id 'java'
    id 'maven-publish'
    id 'non-core-module'
}

ext {
    artifactId = "konna.backend-lwjgl"
    lwjglVersion = "3.3.6"
}

description = "Library frontend implementations using LWJGL for Konna Engine"

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        def osArch = System.getProperty("os.arch")
        if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
            project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
        } else if  (osArch.startsWith("ppc")) {
            project.ext.lwjglNatives += "-ppc64le"
        } else if  (osArch.startsWith("riscv")) {
            project.ext.lwjglNatives += "-riscv64"
        }
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
}

dependencies {

    implementation project(":libfrontend-opengl")
    implementation project(":libfrontend-glfw")
    implementation project(":libfrontend-stb")

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-egl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-opengles"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.lwjgl:lwjgl::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-openal::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-opengles::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-stb::$lwjglNatives"


}
