plugins {
    id 'java'

    id 'distribution'

    id 'jacoco'
    id 'jacoco-report-aggregation'
    id 'maven-publish'

    id 'org.jreleaser' version '1.19.0'
    id 'checkstyle'
}

ext {
    konnaModules = subprojects.findAll {
            it.name.startsWith("component-") // Konna std components
        ||  it.name.startsWith("libfrontend-") // Konna libfrontends
        ||  it.name.startsWith("backend-") // Konna std backends
        ||  it.name.startsWith("implementations-") // Konna std implementations
        ||  it.name == "core" // Konna core
    }
    allKonnaProjects = konnaModules + project(":konna-bom")
}

group = 'io.github.darthakiranihil'

subprojects {
    if (it.name == "konna-bom") {
        return;
    }
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'org.jreleaser'
    apply plugin: 'checkstyle'

    //archivesBaseName = "Konna.${it.name}"
    checkstyle {
        toolVersion = "11.0.0"
        checkstyleTest.enabled = false
    }
}

jacoco {
    toolVersion = "0.8.13"
    reportsDirectory = layout.buildDirectory.dir('reports/jacoco/')
}

checkstyle {
    toolVersion = "11.0.0"
    checkstyleTest.enabled = false
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    konnaModules.each {
        module -> jacocoAggregation(module.toString())
    }
}

distributions {
    main {
        contents {
            from subprojects.collect { it.tasks.withType(Jar) }
        }
    }
}

tasks.register('createJarBundle', Zip) {
    dependsOn build

    from("${rootDir}/build/libs/") {
        exclude "*-sources.jar"
        exclude "*-javadoc.jar"
    }

    destinationDirectory = file("$rootDir/build/distributions")
    archiveFileName = "Konna.bundle-jar-${version}.zip"
}

tasks.register('createJavadocBundle', Zip) {
    dependsOn build

    from("${rootDir}/build/libs/") {
        include "*-javadoc.jar"
    }
    destinationDirectory = file("$rootDir/build/distributions")
    archiveFileName = "Konna.bundle-javadoc-${version}.zip"
}

tasks.register('createSourceBundle', Zip) {
    dependsOn build

    from("${rootDir}/build/libs/") {
        include "*-sources.jar"
    }
    destinationDirectory = file("$rootDir/build/distributions")
    archiveFileName = "Konna.bundle-sources-${version}.zip"
}

tasks.register('createBundle') {
    dependsOn(
        createJarBundle,
        createJavadocBundle,
        createSourceBundle,
    )
}

build {
    finalizedBy createBundle
}


tasks.register('jacocoRootReport', JacocoReport) {
    description = 'Generates an aggregate report from all subprojects'
    dependsOn(konnaModules.test)

    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output)
    executionData = files(konnaModules.jacocoTestReport.executionData)

    reports {
        html.required = true
        xml.required = true
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoRootReport
}



jreleaser {
    gitRootSearch = true
    release {
        github {
            skipRelease = true
            skipTag = true
            sign = true
            branch = "master"
            branchPush = "master"
            overwrite = true
        }
    }
    signing {
        active = 'ALWAYS'
        armored = true
        verify = true
    }
    deploy {
        maven {
            mavenCentral.create("sonatype") {
                active = 'ALWAYS'
                url = "https://central.sonatype.com/api/v1/publisher"
                stagingRepository(layout.buildDirectory.dir("staging-deploy").get().toString())
                setAuthorization("Basic")
                retryDelay = 60
            }
        }
    }
}


jacocoTestReport {
    dependsOn test

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'io/github/darthakiranihil/konna/core/log/**'
            ])
        }))
    }

    reports {
        xml.required =  false
        csv.required = false
        html.required = true
        html.destination file("${layout.buildDirectory}/jacocoHtml")
    }

   // sourceDirectories.from = subprojects.sourceSets.main.allSource.srcDirs.flatten()
}

configure(allKonnaProjects) { project -> {
    apply plugin: "org.jreleaser"
    group = groupId
}}

configure(konnaModules) { project -> {
    apply from: "${rootDir}/gradle/module.gradle"
}}


