plugins {
    id 'java'
    id 'distribution'
    id 'application'
    id 'jacoco'
}

group = 'io.github.darthakiranihil.konna'
version = '0.1.0-SNAPSHOT'

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    archivesBaseName = "Konna.${it.name}"
}

jacoco {
    toolVersion = "0.8.13"
}

application {
    mainClass = 'debug.StrTest' // Replace with your actual main class
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation(project(":core"))
}

test {
    useJUnitPlatform()
    // finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required =  false
        csv.required = false
        html.required = true
        html.destination file("${layout.buildDirectory}/jacocoHtml")
    }
    additionalSourceDirs new File(rootProject.projectDir, 'core/src/main/java')
}

distributions {
    main {
        contents {
            from subprojects.collect { it.tasks.withType(Jar) }
        }
    }
}

def exportedProjects = [
    ":",
    ":core",
]

tasks.register('allJavadoc', Javadoc) {
    source exportedProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(exportedProjects.collect { project(it).sourceSets.main.compileClasspath })
    destinationDir = file("${layout.buildDirectory}/docs/javadoc-all")
}