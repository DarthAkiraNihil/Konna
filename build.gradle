import org.jreleaser.model.Active

plugins {
    id 'java'

    id 'distribution'

    id 'jacoco'
    id 'maven-publish'

    id 'org.jreleaser' version '1.19.0'
    id 'checkstyle'

}

group = 'io.github.darthakiranihil.konna'
version = '0.2.0'

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'org.jreleaser'
    apply plugin: 'checkstyle'

    archivesBaseName = "Konna.${it.name}"
    checkstyle {
        toolVersion = "11.0.0"
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    }
}

jacoco {
    toolVersion = "0.8.13"
    reportsDirectory = layout.buildDirectory.dir('reports/jacoco/')
}

checkstyle {
    toolVersion = "11.0.0"
    checkstyleTest.enabled = false
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation(project(":core"))
}

distributions {
    main {
        contents {
            from subprojects.collect { it.tasks.withType(Jar) }
        }
    }
}

def exportedProjects = [
    ":",
    ":core",
    ":libfrontend-glfw",
    ":libfrontend-gl20",
    ":libfrontend-gl33",
    ":libfrontend-stbimage",
    ":backend-lwjgl",
]

tasks.withType(Javadoc).configureEach {
    options {
        addStringOption('Xdoclint:none', '-quiet')
    }
    options.author = true
    options.tags = [ "apiNote:a:API Note:", "implSpec:a:Implementation Requirements:", "implNote:a:Implementation Note:" ]
}

tasks.register('allJavadoc', Javadoc) {
    source exportedProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(exportedProjects.collect { project(it).sourceSets.main.compileClasspath })
    destinationDir = layout.buildDirectory.dir("/docs/javadoc-all").get().asFile
}

tasks.register('jacocoRootReport', JacocoReport) {
    description = 'Generates an aggregate report from all subprojects'
    dependsOn(subprojects.test)

    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData)

    reports {
        html.required = true
        xml.required = true
    }
}

tasks.register('aggregateCheckstyleReport', Checkstyle) {
    description = 'Generates an aggregated Checkstyle report for all subprojects.'
    group = 'Verification'

    // Collect all Checkstyle reports from subprojects
    source = files(subprojects.collect { it.checkstyleMain.source })
    classpath = files(subprojects.collect { it.checkstyleMain.classpath })

    // Configure the aggregated report output
    reports {
        xml.required = true
        xml.outputLocation = file("${layout.buildDirectory}/reports/checkstyle/aggregate.xml")
        html.required = true
        html.outputLocation = file("${layout.buildDirectory}/reports/checkstyle/aggregate.html")
    }

    // Set the Checkstyle configuration for the aggregated report
    configFile = rootProject.file('config/checkstyle/checkstyle.xml') // Use the same config as subprojects

    // Ensure subproject checkstyle tasks run before aggregation
    dependsOn subprojects.checkstyleMain
}

test {
    useJUnitPlatform()
    finalizedBy jacocoRootReport
}

jreleaser {
    gitRootSearch = true
    release {
        github {
            skipRelease = true
            skipTag = true
            sign = true
            branch = "master"
            branchPush = "master"
            overwrite = true
        }
    }
    signing {
        active = Active.ALWAYS
        armored = true
        verify = true
    }
}

jacocoTestReport {
    dependsOn test

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'io/github/darthakiranihil/konna/core/log/**'
            ])
        }))
    }

    reports {
        xml.required =  false
        csv.required = false
        html.required = true
        html.destination file("${layout.buildDirectory}/jacocoHtml")
    }
//    sourceSets
    sourceDirectories.from = subprojects.sourceSets.main.allSource.srcDirs.flatten()
    // jacocoReportPath = "${layout.buildDirectory}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}
