apply plugin: 'java-library'
apply from: "$rootDir/gradle/deployment.gradle"

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

test {
    useJUnitPlatform()
}

tasks.register('copyJar') {
    doLast {
        copy {
            from 'build/libs'
            into "$rootDir/build/libs"
            include '**/*.jar'
        }
    }
}

tasks.named("build") {
    finalizedBy('copyJar')
}

javadoc {
    description = "Generates project-level javadoc for use in -javadoc jar"
    failOnError = true

    options {
        encoding = "UTF-8"
        memberLevel = JavadocMemberLevel.PROTECTED
        author = true
        header = project.name
        use = true
        // links(project.ext.javadocLinks)
        setOutputLevel(JavadocOutputLevel.QUIET)
        // Check for 'syntax' during linting. Note that the global
        // 'framework-api:javadoc' task checks for 'reference' in addition
        // to 'syntax'.
        addBooleanOption("Xdoclint:syntax,-reference", true)
        // Change modularity mismatch from warn to info.
        // See https://github.com/spring-projects/spring-framework/issues/27497
        addStringOption("-link-modularity-mismatch", "info")
        // With the javadoc tool on Java 25, it appears that the 'reference'
        // group is always active and the '-reference' flag is not honored.
        // Thus, we do NOT fail the build on Javadoc warnings due to
        // cross-module @see and @link references which are only reachable
        // when running the global 'framework-api:javadoc' task.
        addBooleanOption('Werror', false)
    }

    // Attempt to suppress warnings due to cross-module @see and @link references.
    // Note that the global 'framework-api:javadoc' task displays all warnings.
    logging.captureStandardError LogLevel.INFO
    logging.captureStandardOutput LogLevel.INFO  // suppress "## warnings" message
}

